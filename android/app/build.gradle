plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

// Define the major and minor version manually
def majorVersion = 1
def minorVersion = 5  // You can manually update this for each release

// Get the build number from the environment variable (set in GitHub Actions)
def buildNumber = System.getenv("BUILD_NUMBER") ?: '1'

// Create the version name using major, minor, and build numbers
def buildVersionName = "${majorVersion}.${minorVersion}.${buildNumber}"

android {

    namespace "com.makinglifeeasie.closetconscious"
    compileSdk flutter.compileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.makinglifeeasie.closetconscious"
        minSdkVersion 27
        targetSdkVersion 34
        versionCode buildNumber.toInteger()  // Automatically use build number for versionCode
        versionName buildVersionName         // Version name format: "1.0.123"
    }
    flavorDimensions "default"

    productFlavors {
        dev {
            applicationId "com.makinglifeeasie.closetconscious.dev"
            versionNameSuffix "-dev"
        }
        prod {
            applicationId "com.makinglifeeasie.closetconscious"
        }
    }

    signingConfigs {
        debug {
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storeFile file('dev_keystore.jks')
            storePassword keystoreProperties['debugStorePassword']
            storeType "JKS"
        }

        release {
            keyAlias keystoreProperties['prodKeyAlias']
            keyPassword keystoreProperties['prodKeyPassword']
            storeFile file('prod_keystore.jks')
            storePassword keystoreProperties['prodStorePassword']
            storeType "JKS"
        }
    }

    buildTypes {

        debug {
            signingConfig signingConfigs.debug
            debuggable true
            shrinkResources false
        }

        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }
}


flutter {
    source '../..'
}
